apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.gms.google-services"

android {
    buildscript {
        apply from: '../dependencies.gradle'
    }

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "tk.lorddarthart.weathertest"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        versionCode rootProject.version_build
        versionName "${rootProject.version_major}" +
                ".${rootProject.version_minor}" +
                ".${rootProject.version_patch}-b${rootProject.version_build}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            versionNameSuffix "-release"
        }
        debug {
            versionNameSuffix "-dev"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith(".apk")) {
                def mFileName = "weatherTestApp-v${versionName}.apk"
                outputFileName = mFileName
            }
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:$appcompatVersion"

    // Testing
    implementation "androidx.legacy:legacy-support-v4:$testingLegacySupportVersion"
    testImplementation "junit:junit:$testingJunitVersion"
    androidTestImplementation "androidx.test.ext:junit:$testingAndroidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testingEspressoVersion"

    // Design
    implementation "androidx.constraintlayout:constraintlayout:$designConstraintLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$designRecyclerViewVersion"
    implementation "androidx.cardview:cardview:$designCardViewVersion"
    implementation "com.google.android.material:material:$designMaterialVersion"

    // Firebase
    implementation "com.google.firebase:firebase-auth:$firebaseAuthVersion"

    // CircleImageView
    implementation "de.hdodenhof:circleimageview:$circleImageViewVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Anko
    implementation "org.jetbrains.anko:anko:$ankoVersion"
    implementation "org.jetbrains.anko:anko-design:$ankoVersion"

    // PullToRefresh
    implementation "com.baoyz.pullrefreshlayout:library:$p2rVersion"

    // RxJava2 & RxAndroid2
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // Retrofit2 + Converter_GSON + Adapter_RxJava2
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // OkHTTP3
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Jetpack
    implementation "androidx.preference:preference-ktx:$androidxPreferenceVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtensionsVersion"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycleExtensionsVersion"

    // Dagger2
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Moxy
    implementation "com.github.moxy-community:moxy:$moxyVersion"
    implementation "com.github.moxy-community:moxy-material:$moxyVersion"
    implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
    kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Cicerone
    implementation "ru.terrakok.cicerone:cicerone:$ciceroneVersion"
}

repositories {
    mavenCentral()
}
